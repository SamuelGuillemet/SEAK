plugins {
    id "com.github.johnrengelman.shadow"
    id "io.micronaut.application"
}

version = "${version}"
group = "pfe_broker"

repositories {
    mavenCentral()
    maven { url confluentUrl }
}

dependencies {
    implementation project(":libs:log")
    implementation project(":libs:avro")
    implementation project(":libs:models")
    implementation project(":libs:common")

    runtimeOnly "org.yaml:snakeyaml"
    implementation("io.micronaut.kafka:micronaut-kafka")
    implementation("io.micronaut:micronaut-runtime")

    implementation("io.micronaut.data:micronaut-data-hibernate-jpa")

    // Log4J
    implementation("org.apache.logging.log4j:log4j-core")
    runtimeOnly("org.apache.logging.log4j:log4j-api")
    runtimeOnly("org.apache.logging.log4j:log4j-slf4j2-impl")

    // Avro
    implementation group: 'io.confluent', name: 'kafka-avro-serializer', version: '7.5.1'

    // QuickFixJ
    implementation group: 'org.quickfixj', name: 'quickfixj-core', version: '2.3.1'
    implementation group: 'org.quickfixj', name: 'quickfixj-messages-all', version: '2.3.1'

    // Test implementation
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.19.3'
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.19.3'
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.2.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.8.0'
}

application {
    mainClass.set("pfe_broker.quickfix_server.MainApplication")
}

java {
    sourceCompatibility = JavaVersion.toVersion("${javaVersion}")
    targetCompatibility = JavaVersion.toVersion("${javaVersion}")
}

sourceSets {
    main {
        resources {
            srcDirs = [
                "src/main/resources",
                project(":").file("config/common").path,
                project(":").file("config/quickfix").path,
                project(":").file("config/log").path
            ]
        }
    }
    test {
        resources {
            srcDirs = [
                "src/test/resources",
                project(":").file("config/common").path,
                project(":").file("config/quickfix").path,
                project(":").file("config/log").path
            ]
        }
    }
}


graalvmNative.toolchainDetection = false

micronaut {
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("pfe_broker.quickfix_server.*")
    }
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
}
