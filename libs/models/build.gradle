plugins {
  id "io.micronaut.minimal.library"
  id 'io.freefair.lombok'
}

ext {
    configFiles = "classpath:data.yml"
}

group = 'pfe_broker'
version = "${version}"

repositories {
    mavenCentral()
}

dependencies {
    // Micronaut
    runtimeOnly libs.snakeyaml

    // Database dependencies
    annotationProcessor libs.micronaut.data.processor
    implementation libs.micronaut.data.hibernate.jpa
    implementation libs.micronaut.jdbc.hikari
    runtimeOnly libs.postgresql

    // Authentication dependencies
    implementation libs.spring.security.crypto

    // Lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    // Log4j2
    runtimeOnly libs.log4j.api
    runtimeOnly libs.log4j.slf4j2.impl
    runtimeOnly libs.jcl.over.slf4j

    // Test dependencies
    testImplementation libs.micronaut.test.junit5
    testImplementation libs.assertj.core
    testImplementation libs.testcontainers.postgresql
    testImplementation libs.testcontainers.junit.jupiter
}

java {
    sourceCompatibility = JavaVersion.toVersion("${javaVersion}")
    targetCompatibility = JavaVersion.toVersion("${javaVersion}")
}

sourceSets {
    test {
        resources {
            srcDirs = ["src/test/resources", project(":").file("config/common").path]
        }
    }
}

micronaut {
  testRuntime("junit5")
  processing {
    incremental(true)
    annotations("pfe_broker.models.*")
  }
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    systemProperty 'micronaut.config.files', configFiles
}
